DELIMITERS = "<.>" ;

LIST V = vblex ;
LIST Vbmod = vbmod ;
LIST A = adj ;
LIST Pr = pr ;
LIST Det = det ;
LIST Prn = prn ;
LIST N = n ;
LIST Detnt = detnt ;

LIST Inf = inf ;
LIST PrfPrc = pp ;

LIST Pro = pro ;
LIST Enc = enc ;

LIST Masc = m ;
LIST Fem = f ;
LIST MascFem = mf ;

LIST Pri = pri ;
LIST Pii = pii ;
LIST Pis = pis ;
LIST Prs = prs ;
LIST Fti = fti ;
LIST Fts = fts ;

LIST Imp = imp ;


LIST Ser = vbser ;
LIST Haver = vbhaver ;

LIST Poder = "poder" ;
LIST Para = "para" ;
LIST De = "de" ;
LIST Entre = "entre" ;
LIST Outro = "outro" ;
LIST Se = "se" ; 
LIST Que = "que" ; 
LIST Estar = "estar" ;
LIST Ir = "ir" ; 

LIST Foi = "<foi>"i ;
LIST Vai = "<vai>"i ;
LIST É = "<é>"i ;
LIST Uma = "<uma>"i ;
LIST Sido = "<sido>"i ;
LIST Sua = "<sua>"i ;

SET Fin = Pri | Prs | Pii | Pis | Fti | Fts ; 
SET Gender = Fem + MascFem | Masc + MascFem;

SECTION

SELECT Inf IF (-1 Para OR De) (0C V) ;


SELECT Ser IF (0 Foi) (1 PrfPrc) ;

SELECT Pr IF (0 Entre) (1C N) ;

SELECT Inf IF (-1C Vai) ;

SELECT Det IF (-1C É) (0 Det OR Prn) (1C N) ;

REMOVE Pro IF (NOT 1 V OR Prn) (NOT -1 Inf) ;

SELECT Det IF (0 Uma) (1 Outro LINK 1 N) ;

SELECT Pro IF (-1 Pr) (0 Se) (1 Inf) ;

SELECT Pro IF (-2 Pr) (-1 Inf) (0 Se) ;

REMOVE Detnt IF (NOT 1 Que) ;

REMOVE Pr IF (1C Fin) ;

REMOVE Pr IF (-1C Pr) ;

REMOVE Pr IF (-1 Entre) ;

SELECT Ser IF (1C PrfPrc) ; 

SELECT N IF (-1 Det) (0C N OR A) (1C A) ;

REMOVE Det IF (1C Fin) ;

SELECT PrfPrc IF (-1C Estar) ;

SELECT Det IF (-1C Pr) (0C Det OR Prn) (1C N);

REMOVE Imp IF (-1C Se) ;

SELECT N IF (-1C Det) (1 N OR Inf) ;

SELECT PrfPrc IF (-1 Ser) (0C A OR PrfPrc) ;

SELECT N + $$Gender IF (-1C Det + $$Gender) (0C N OR V) ;

SELECT Haver IF (1C Sido) ;

SELECT Vbmod IF (0 Poder) (1 Inf) ;

SELECT Det IF (-1 Pr) (0 Sua) (1 N) ;

SELECT Ser IF (0C Ser OR Ir) (1 A) ;
